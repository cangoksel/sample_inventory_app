group = "com.github.cangoksel"
apply from: "$rootDir/gradle/versioning.gradle"
apply plugin: "java"
apply plugin: "groovy"
apply plugin: "jacoco"
apply plugin: "org.springframework.boot"
apply plugin: "idea"
apply from: "$rootDir/gradle/provided.gradle"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
[compileGroovy, compileTestGroovy]*.options*.encoding = 'UTF-8'

configurations {
    compile.exclude module: 'spring-boot-starter-logging'
}

configurations.all({
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == "org.codehaus.groovy") {
                details.useVersion "${groovyVersion}"
            }

            if (details.requested.group == "org.hibernate" && details.requested.name == "hibernate-entitymanager") {
                details.useVersion "${hibernateVersion}"
            }
        }
    }
})

jacoco {
    toolVersion = "0.7.4.201502262128"
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

repositories {
    jcenter()

}
sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.junit.reportsPath", "$buildDir/test-results/"
        property "sonar.groovy.binaries", "$buildDir/jacoco/"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(GroovyCompile) {
    options.encoding = 'UTF-8'
}
dependencies {
    compile project(":domain")
    compile('org.hibernate:hibernate-entitymanager')
    compile('org.hibernate:hibernate-envers')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-data-mongodb')
    compile('org.springframework.boot:spring-boot-starter-mail')
    compile('org.springframework.boot:spring-boot-starter-security')

    compile("org.mnode.ical4j:ical4j:2.0-beta1") {
        exclude(group: "biz.aQute.bnd", module: "bndlib")
    }

    compile("org.apache.camel:camel-spring-boot-starter:${camelVersion}")
    compile("org.apache.camel:camel-cxf:${camelVersion}") {
        exclude(group: 'org.apache.cxf', module: 'cxf-rt-rs-security-oauth')
    }
    compile("org.apache.cxf:cxf-rt-rs-client:${cxfVersion}")


    compile("com.squareup.retrofit2:retrofit:${retrofitVersion}")
    compile("com.squareup.retrofit2:converter-jackson:${retrofitVersion}")
    compile("org.apache.tika:tika-core:1.13")

    // Logging Dependencies
    //compile('org.springframework.boot:spring-boot-starter-log4j2')
    compile("ch.qos.logback.contrib:logback-mongodb-classic:0.1.2")
    compile("com.google.guava:guava:${guavaVersion}")

    compile("javax.money:money-api:${javaMoneyVersion}")
    compile("org.javamoney:moneta:${javaMoneyVersion}")
    compile("org.freemarker:freemarker:${freemarkerVersion}")

    runtime("org.apache.camel:camel-jaxb:${camelVersion}")
    runtime("org.apache.camel:camel-http4:${camelVersion}")
    runtime("org.apache.camel:camel-quartz2:${camelVersion}")
    runtime("org.apache.camel:camel-jpa:${camelVersion}")
    runtime("org.apache.camel:camel-mail:${camelVersion}") {
        exclude(group: "javax.mail", module: "mail")
    }

    provided("org.projectlombok:lombok:$lombokVersion")

    // Testing Dependencies
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile("org.codehaus.groovy:groovy-all:${groovyVersion}")
    testCompile("com.relevantcodes:extentreports:${extentReportVersion}")
    testCompile("org.spockframework:spock-core:${spockVersion}") {
        exclude(module: 'junit-dep')
    }
    testCompile("org.spockframework:spock-spring:${spockVersion}")
    testCompile("com.anotherchrisberry:spock-retry:0.6.1")
    testCompile("org.apache.camel:camel-test:${camelVersion}")
    testRuntime("javax.servlet:javax.servlet-api:3.1.0")
    testRuntime("cglib:cglib-nodep:${cglibNodepVersion}")
}